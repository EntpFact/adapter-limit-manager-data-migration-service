plugins {
    id 'java-library'
    id 'maven-publish'
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
    alias(libs.plugins.diffplug.spotless)
    id 'jacoco'
}

group 'com.hdfcbank.limitmanager'
version '0.0.1-SNAPSHOT'
archivesBaseName = 'shikhar-limit-manager-adapter-data-migration-service'

java {
    sourceCompatibility = '21'
}

configurations.implementation {
    exclude group: "org.xmlunit", module: "xmlunit-core"
}

dependencies {
    implementation libs.spring.boot.starter
    implementation libs.dapr.vault.lib
    implementation libs.spring.web
    implementation libs.spring.core
    implementation libs.spring.context
    implementation libs.commons.io
    testImplementation(libs.spring.boot.starter.test){
        exclude group: 'com.jayway.jsonpath', module: 'json-path'
    }

    implementation libs.flyway.core
    runtimeOnly libs.postgresql
    implementation libs.spring.boot.starter.data.jdbc
    implementation libs.commons.lang3
    annotationProcessor libs.project.lombok
    compileOnly libs.project.lombok
    implementation libs.spring.data.yugabytedb.ysql
    implementation libs.jdbc.yugabytedb
    testImplementation (libs.yugabytedb){
        exclude group: 'org.apache.commons', module: 'commons-compress'
    }
    implementation libs.postgresql
    testImplementation libs.junit.jupiter
    testImplementation (libs.junit.jupiter){
        exclude group: 'org.apache.commons', module: 'commons-compress'
    }
}

publishing {
    repositories {
        maven {
            name "maven"
            url "artifactregistry://us-central1-maven.pkg.dev/issuer-switch/maven-artifacts"
        }
        mavenLocal()
    }
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.hdfcbank.limitmanager'
            artifactId 'shikhar-limit-manager-adapter-data-migration-service'
            version '0.0.1-SNAPSHOT'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

test {
    useJUnitPlatform()
}

compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

jacocoTestReport {
    reports {
        xml.required= true
        csv.required= false
        html.required= true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                              'com/hdfcbank/limitmanager/LimitManagerDataMigrationApp.**'])
        }))
    }
}
test.finalizedBy jacocoTestReport

ext {
    set("jakarta-servlet.version", '6.0.0')
}
